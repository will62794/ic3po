#lang ivy1.7

type node
type value
type quorum
type phase

relation member(N:node, Q:quorum)
axiom forall Q1:quorum, Q2:quorum. exists N:node. member(N, Q1) & member(N, Q2)

relation vote_request_msg(N0:node, N1:node)
relation voted(N:node)
relation vote_msg(N0:node, N1:node)
relation votes(N0:node, N1:node)
relation leader(N:node)
relation decided(N:node, V:value)

after init  {
	vote_request_msg(N1, N2) := true;
  
	vote_msg(N1, N2) := *;
  voted(N) := *;

	votes(N1, N2) := *;

	leader(N1) := *;
	decided(N, V) := false;

  # The invariant of the previous 'sendVote' phase holds.
  require (forall I, J, S . (~(I = J) -> (vote_msg(S, I) -> ~vote_msg(S, J))));
  require (forall N0, N1 . (vote_msg(N0, N1) -> voted(N0)));

  # The invariant of the 'recvVote' phase holds.
  require forall I,J. votes(I,J) -> vote_msg(J,I);

  # The invariant of the 'becomeLeader' phase holds.
  require (forall I, J . ( (leader(I) & leader(J)) -> (I = J)) );
  require (exists Q0 . (forall N1, N2, N0 . ((leader(N0) & member(N1, Q0) & vote_msg(N1, N2)) -> (N0 = N2))));
}

action decide(n: node, v: value) = {
  require leader(n);
  require ~decided(n, V);
  
  decided(n,v) := true;
}

export decide

# (Phase invariant)
# At most one value is decided upon.
invariant [safety] (decided(N1, V1) & decided(N2, V2) -> V1 = V2)